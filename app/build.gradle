plugins {
    id 'com.android.application'
}

android {
    applicationVariants.all { variant ->
        //variant.packageApplicationProvider.get().outputDirectory = new File(project.getRootDir().absolutePath + "/" + variant.buildType.name + "/")
        variant.outputs.all { output ->
            output.outputFileName = "FMRadio" + (variant.buildType.name.toLowerCase().contains("release") ? "" : "-" + variant.buildType.name) + ".apk"
        }
    }
    signingConfigs {
        release {
            def f = file("../local.properties")
            if (f.exists()) {
                def props = new Properties()
                f.withInputStream { props.load(it) }
                storeFile file(props.RELEASE_KEY_PATH)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS
                keyPassword props.RELEASE_KEY_PASSWORD
            } else {
                def writer = new FileWriter(f.path)
                writer.write("RELEASE_KEY_PATH=\n")
                writer.write("RELEASE_KEY_PASSWORD=\n")
                writer.write("RELEASE_KEY_ALIAS=\n")
                writer.write("RELEASE_STORE_PASSWORD=")
                writer.close()
                throw new IOException("File $f.name was created. please fill it by values of your release key.")
            }

        }
    }
    compileSdk 26
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.android.fmradio"
        minSdk 21
        targetSdk 26
        versionCode 2
        versionName "2.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    
    compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
	}
    
    lintOptions {
        checkReleaseBuilds false
        //If you want to continue even if errors found use following line
        abortOnError false
    }
    
}

dependencies {
     implementation 'com.android.support:appcompat-v7:26.0.0'
}
